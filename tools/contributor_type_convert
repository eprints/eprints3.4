#!/usr/bin/perl -w

use FindBin;
use lib "$FindBin::Bin/../perl_lib";

=head1 NAME

contributor_type_convert - Convert contributor type values in contributors and contributions fields (as applicable) to new http://id.loc.gov/vocabulary/relators/... URIs.

=head1 SYNOPSIS

contributor_type_convert [OPTIONS] repository_id [ <dataset> <field> <field> ... ]

=head1 OPTIONS

=over 8

=item --help

Show this help text.

=item --verbose

Output more detail about changes being made.

=item --quiet

Do not make any output.

=back

=cut

use EPrints;

use strict;
use Getopt::Long;
use Pod::Usage;

my $verbose = 0;
my $quiet = 0;
my $help = 0;
my $man = 0;

Getopt::Long::Configure("permute");

GetOptions(
    'help|?' => \$help,
    'verbose+' => \$verbose,
    'quiet' => \$quiet
) || pod2usage( 2 );
pod2usage( 1 ) if $help;
pod2usage( -exitstatus => 0, -verbose => 2 ) if $man;
pod2usage( 2 ) if( scalar @ARGV < 1 );

my $noise = 1;
$noise = 0 if( $quiet );
$noise = 1+$verbose if( $verbose );

# Set STDOUT to auto flush (without needing a \n)
$|=1;

my $repoid = $ARGV[0];
my $session = new EPrints::Session( 1 , $repoid , $noise );
if( !defined $session )
{
    print STDERR "Failed to load repository: $repoid\n";
    exit 1;
}

my $dataset_id = $ARGV[1] ? $ARGV[1] : 'eprint';
my $info = { count => 0 };
$info->{fields} = scalar @ARGV > 2 ? [ @ARGV[2..$#ARGV] ] : [ 'contributors' ];


my $dataset = $session->get_repository->get_dataset( $dataset_id );
$dataset->search->map( \&convert_contributor_types, $info );

if( $noise > 0 )
{
    {
        print "Converted contributor types for ".$info->{count}." eprints.\n";
    }
}
$session->terminate();
exit;

sub convert_contributor_types
{
    my( $session, $dataset, $eprint, $info ) = @_;

    my $contributor_types_change = 0;

	foreach my $field ( @{$info->{fields}} )
	{
        my $values = $eprint->get_value( $field );
        for ( my $c = 0; $c < scalar @$values; $c++ )
        {
            my $new_value = $values->[$c]->{type};
			next unless $new_value;
            $new_value =~ s!www.loc.gov/loc.terms/!id.loc.gov/vocabulary/!;
            $new_value =~ s!relators/([A-Z]+)!relators/\L$1!;
            if ( $new_value ne $values->[$c]->{type} )
            {
                $values->[$c]->{type} = $new_value;
                $contributor_types_change = 1;
            }
        }
        $eprint->set_value( $field, $values ) if $contributor_types_change;
    }

    return unless $contributor_types_change;

    if( $noise > 1 )
    {
        print "Updated contributor_types for eprint #".$eprint->get_value( "eprintid" )."\n";
    }
    $eprint->commit;
    $info->{count}++;
}
